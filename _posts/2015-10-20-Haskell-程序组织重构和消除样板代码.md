---
layout: post 
category: haskell
tags: [ read , haskell ]
---
{% include JB/setup %}
#Haskell

###消除样板代码
{% highlight haskell %}
(>>?) :: Maybe a -> (a -> Maybe b) -> Maybe b
Nothing >>? _ = Nothing
Just v  >>? f = f v

parseP5_take2 :: L.ByteString -> Maybe (Greymap, L.ByteString)
parseP5_take2 s =
    matchHeader (L8.pack "P5") s      >>?
    \s -> skipSpace ((), s)           >>?
    (getNat . snd)                    >>?
    skipSpace                         >>?
    \(width, s) ->   getNat s         >>?
    skipSpace                         >>?
    \(height, s) ->  getNat s         >>?
    \(maxGrey, s) -> getBytes 1 s     >>?
    (getBytes (width * height) . snd) >>?
    \(bitmap, s) -> Just (Greymap width height maxGrey bitmap, s)

skipSpace :: (a, L.ByteString) -> Maybe (a, L.ByteString)
skipSpace (a, s) = Just (a, L8.dropWhile isSpace s)

{% endhighlight %}

###隐式状态

####解析状态
* 为了以防代码更新，需要修改很多，设计解析状态类型封装
{% highlight haskell %}
data ParseState = ParseState {
      string :: L.ByteString
    , offset :: Int64           -- imported from Data.Int
    } deriving (Show)
{% endhighlight %}
* 用了`记录语法`：现在可以避免使用模式匹配来获取状态信息了，可以用 string 和 offset `访问函数`。
* 记录语法的用处不仅仅在于访问函数：我们可以用它来`复制或部分改变`已有值
{% highlight haskell %}
modifyOffset :: ParseState -> Int64 -> ParseState
modifyOffset initState newOffset =
    initState { offset = newOffset }
{% endhighlight %}
在大括号里我们可以给任意多的字段赋值，用`逗号`分开即可。

####解析类型（构建解析器）
* 它处理一个解析状态，产生新解析状态和一些别的信息。 我们可以用 Haskell 类型直接表示。 
{% highlight haskell %}
betterParse :: ParseState -> Either String (a, ParseState)
betterParse = undefined
{% endhighlight %}
* 最好`不要`将解析器的实现`暴露`给用户,我们用一个 newtype 声明来隐藏解析器的细节。
{% highlight haskell %}
newtype Parse a = Parse {
    runParse :: ParseState -> Either String (a, ParseState)
    }	
{% endhighlight %}
newtype 只是函数在编译时的一层包装，它`没有运行时开销`。我们想用这个函数时，我们用 runParser 访问器。

* 模块`不`导出 Parse `值构造器`，我们就能确保没人会不小心创建一个解析器，或者通过模式匹配来观察其内部构造。所有的解析器都是作者自己创建

####解析
{% highlight haskell %}
parse :: Parse a -> L.ByteString -> Either String a
parse parser initState
    = case runParse parser (ParseState initState 0) of
        Left err          -> Left err
        Right (result, _) -> Right result
{% endhighlight %}

####<font color="red">复杂解析器</font>
{% highlight haskell %}
parseByte :: Parse Word8
parseByte =
    getState ==> \initState ->
    case L.uncons (string initState) of
      Nothing ->
          bail "no more input"
      Just (byte,remainder) ->
          putState newState ==> \_ ->
          identity byte
        where newState = initState { string = remainder,
                                     offset = newOffset }
              newOffset = offset initState + 1

getState :: Parse ParseState
getState = Parse (\s -> Right (s, s))

putState :: ParseState -> Parse ()
putState s = Parse (\_ -> Right ((), s))

bail :: String -> Parse a
bail err = Parse $ \s -> Left $
           "byte offset " ++ show (offset s) ++ ": " ++ err
           
(==>) :: Parse a -> (a -> Parse b) -> Parse b

firstParser ==> secondParser  =  Parse chainedParser
  where chainedParser initState   =
          case runParse firstParser initState of
            Left errMessage ->
                Left errMessage
            Right (firstResult, newState) ->
                runParse (secondParser firstResult) newState

{% endhighlight %}

* getState 来得到解析状态的副本，然后调用 putState 将当前状态更新为新状态。
* 将解析状态的细节打包放入 ParseState 类型中，然后我们通过访问器而不是模式匹配来访问它。 隐式地传递解析状态给我们带来另外的好处。如果想增加解析状态的信息，我们只需修改 ParseState 定义，以及需要新信息的函数体即可。 跟之前通过模式匹配暴露状态的解析器相比，现在的代码更加模块化：只有需要新信息的代码会受到影响。
* `(==>)` 它仅仅创建了一个闭包（closure）用来记忆 firstParser 和 secondParser 的值。

####给 Parse 写一个 Functor 实例
{% highlight haskell %}
instance Functor Parse where
    fmap f parser = parser ==> \result ->
                    identity (f result)
{% endhighlight %}
然后验证Functor实例的两条规则是否满足
{% highlight haskell %}
ghci> parse parseByte input
Right 102
ghci> parse (id <$> parseByte) input
Right 102
ghci> parse ((chr . fromIntegral) <$> parseByte) input
Right 'f'
ghci> parse (chr <$> fromIntegral <$> parseByte) input
Right 'f'
{% endhighlight %}

* Functor 它让我们写出简洁、表达能力强的代码,经常想用 ASCII 字符而不是 Word8 值,尽管可以写一个类似于 parseByte 的 parseChar 函数，我们现在可以利用 Parse 的 Functor 属性来`避免重复代码`。
{% highlight haskell %}
-- file: ch10/Parse.hs
w2c :: Word8 -> Char
w2c = chr . fromIntegral

-- import Control.Applicative
parseChar :: Parse Char
parseChar = w2c <$> parseByte
{% endhighlight %}

{% highlight haskell %}

{% endhighlight %}





